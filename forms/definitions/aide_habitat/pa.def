formulaire metadata_name_aide_habitat_pa 

    url metadata_url_name_aide_habitat_pa
    description metadata_description_aide_habitat_pa
    identifiant metadata_internal_identifier_aide_habitat_pa

    listing type_proprietaire_list
    listing revenu_fiscal_reference_beneficiaire

    filter type_proprietaire_list
    filter revenu_fiscal_reference_beneficiaire

page page_preambule

	texte page_preambule:intro_textes_obligatoires

    liste* qui_saisit_list

    texte qui_saisit_conseil

page page_eligibilite

    si annee_de_construction contient "Depuis" alors afficher le message MSG_NONEL_ANNEECONSTRUCTION
    
    si eligibilite_beneficiaire_apa est égal à "Non" alors afficher le message MSG_NONEL_NONAPA

    si eligibilite_beneficiaire_retraite est égal à "Oui" alors afficher le message MSG_NONEL_CAISSERETRAITE

    si beneficiaire:codepostal ne commence pas par "44" alors afficher le message MSG_NONEL_HABITANTDELA

    si travaux_a_engager_ascenceur_pa est vide et travaux_a_engager_amenagement_pa est vide et travaux_a_engager_passage_pa est vide et travaux_a_engager_extension_pa est vide alors afficher le message MSG_ELIGI_TRAVAUX

    texte page_eligibilite:intro_textes_obligatoires

    titre beneficiaire

    liste* type_proprietaire_list

    texte type_proprietaire_aide

    liste* eligibilite_beneficiaire_apa

    liste* eligibilite_beneficiaire_retraite

    liste* beneficiaire:civilite
    
    nom_prenom* beneficiaire:nom beneficiaire:prenom
    
    si qui_saisit_list contient "ou un de ses proches" alors préremplir beneficiaire:nom avec profil:nom

    si qui_saisit_list contient "ou un de ses proches" alors préremplir beneficiaire:prenom avec profil:prenom

    courriel courriel

    adresse* beneficiaire:adresse beneficiaire:codepostal beneficiaire:commune

    titre logement

    annee_avant_depuis* annee_de_construction 15

    liste* subvention_obtenue

    texte precedent_dossier_texte

    champ_texte precedent_dossier

    titre travaux_a_engager_titre

    texte travaux_a_engager_texte

    texte travaux_a_engager_texte_moteur_recherche_operateur

    liste_choix_multiple travaux_a_engager_ascenceur_pa

    liste_choix_multiple travaux_a_engager_amenagement_pa

    liste_choix_multiple travaux_a_engager_passage_pa

    liste_choix_multiple travaux_a_engager_extension_pa

    champ_texte_long travaux_complement_informations

    titre ressources_occupant

    texte eligibilite_revenus_tres_modestes

    montant* revenu_fiscal_reference_beneficiaire

    montant revenu_fiscal_reference_conjoint
        aide à la saisie revenu_fiscal_reference_conjoint_remarque

    montant revenu_fiscal_autre

    nombre* nombre_occupants

page page_complement_info
   
    texte page_complement_info:intro_textes_obligatoires

    titre autre_occupant

    liste autre_occupant:civilite

    champ_texte autre_occupant:nom
  
    champ_texte autre_occupant:prenom

    titre procuration_titre

    liste* procuration_perception_subventions

    titre tel_beneficiaire
    
    texte MSG_COMPIN_TELEPHONE
    
    telephone tel_fixe

    telephone tel_portable

    texte MSG_COMPIN_SMS

    titre complement_titre_referent_operateur

    si qui_saisit_list contient "ou un de ses proches" alors masquer le champ complement_titre_referent_operateur

    champ_texte* complement_referent_operateur

    si qui_saisit_list contient "ou un de ses proches" alors masquer le champ complement_referent_operateur

    # Messages d'erreurs

    si tel_fixe est vide et tel_portable est vide alors afficher le message MSG_COMPIN_TELEPHONE

    si procuration_perception_subventions contient "fait une procuration en ligne" et tel_portable n'est pas rempli alors afficher le message MSG_COMPIN_SMS

page page_plan_financement

    si depenses_autres_precisions est vide et depenses_autres est rempli alors afficher le message MSG_PLFIN_AUTRECOUTS

    si subventions_autres_precisions est vide et subventions_autres est rempli alors afficher le message MSG_PLFIN_AUTREFINANCE

	texte page_plan_financement:intro_textes_obligatoires

    titre depenses_titre

    montant* depenses_cout_envisages

    montant depenses_ergotherapeute

    montant depenses_amo

    montant depenses_autres

    champ_texte_long depenses_autres_precisions

    montant* depenses_total_ht

    montant* depenses_total_ttc

    titre subventions_titre

    montant* subventions_departement

    montant subventions_anah_aide

    montant subventions_anah_amo

    montant subventions_prime_habiter_mieux

    montant subventions_mdph

    montant subventions_region

    montant subventions_commune

    montant subventions_caf

    montant subventions_caisse_retraite

    montant subventions_autres

    champ_texte_long subventions_autres_precisions

    titre apport_titre

    montant apport

page page_procuration

    si procuration_perception_subventions contient "ne fait pas de procuration" alors masquer cette page

	texte page_procuration:intro_textes_obligatoires

    titre procuration:procuration_titre

    texte procuration:procuration_titre_texte

    titre procuration_soustitre_beneficiaire

    texte procuration_rappel_coordonnees_beneficiaire

    titre procuration_soustitre_represente

    liste procurationrepresentant:civilite

    nom_prenom procurationrepresentant:nom procurationrepresentant:prenom

    champ_texte procurationrepresentant:qualite
        aide à la saisie qualite_aide

    adresse procurationrepresentant:adresse procurationrepresentant:codepostal procurationrepresentant:commune

    titre procuration_donne_mandat_titre

    liste* procuration_donne_mandat_list

page page_mandat_organisme

    si procuration_donne_mandat_list n'est pas égal à "Un organisme ou un opérateur" alors masquer cette page

    texte page_mandat_organisme:intro_textes_obligatoires

    titre procuration_donne_mandat_organisme_sous_titre

    champ_texte* procuration_nom_organisme

    champ_texte* procuration_referent_organisme

    adresse* organisme:adresse organisme:codepostal organisme:commune

    champ_texte* page_mandat_organisme:procuration_sms_telephone

    si procuration_perception_subventions n'est pas égal à "Le bénéficiaire fait une procuration en ligne validée par SMS" alors masquer le champ page_mandat_organisme:procuration_sms_telephone

    texte page_mandat_organisme:procuration_sms_description

    si procuration_perception_subventions n'est pas égal à "Le bénéficiaire fait une procuration en ligne validée par SMS" alors masquer le champ page_mandat_organisme:procuration_sms_description

    texte page_mandat_organisme:procuration_courrier_description

    si procuration_perception_subventions n'est pas égal à "Le bénéficiaire fait une procuration papier validée par l'envoi d'un courrier d'un original signé" alors masquer le champ page_mandat_organisme:procuration_courrier_description

page page_mandat_personne

    si procuration_donne_mandat_list n'est pas égal à "Une personne" alors masquer cette page

    texte page_mandat_personne:intro_textes_obligatoires

    titre procuration_donne_mandat_personne_sous_titre

    nom_prenom_civilite* mandant:nom mandant:prenom mandat:civilite

    champ_texte procuration:qualite
        aide à la saisie qualite_aide

    adresse* mandant:adresse mandant:codepostal mandant:commune

    champ_texte* page_mandat_personne:procuration_sms_telephone

    si procuration_perception_subventions n'est pas égal à "Le bénéficiaire fait une procuration en ligne validée par SMS" alors masquer le champ page_mandat_personne:procuration_sms_telephone

    texte page_mandat_personne:procuration_sms_description

    si procuration_perception_subventions n'est pas égal à "Le bénéficiaire fait une procuration en ligne validée par SMS" alors masquer le champ page_mandat_personne:procuration_sms_description

    texte page_mandat_personne:procuration_courrier_description

    si procuration_perception_subventions n'est pas égal à "Le bénéficiaire fait une procuration papier validée par l'envoi d'un courrier d'un original signé" alors masquer le champ page_mandat_personne:procuration_courrier_description

page page_engagement_operateur

    si qui_saisit_list contient "ou un de ses proches" alors masquer cette page

    si engagement_operateur est vide alors afficher le message MSG_ENGAGEMENT

	texte page_engagement_operateur:intro_textes_obligatoires

    texte engagement_operateur_texte

    liste_choix_multiple* engagement_operateur

    texte engagement_operateur_modele_accord

page page_engagement_beneficiaire

    si qui_saisit_list ne contient pas "ou un de ses proches" alors masquer cette page

    si engagement_beneficiaire est vide alors afficher le message MSG_ENGAGEMENT

    texte engagement_beneficiaire_texte

    liste_choix_multiple* engagement_beneficiaire

page page_pieces_justificatives

	texte page_pieces_justificatives:intro_textes_obligatoires

    fichier* documents_rib

    texte documents_rib_texte

    fichier* documents_apa

    fichier* documents_diagnostic_ergotherapeute

    texte documents_avis_imposition_texte

    fichier* documents_avis_imposition_beneficiaire

    fichier documents_avis_imposition_conjoint

    si revenu_fiscal_reference_conjoint est vide alors masquer le champ documents_avis_imposition_conjoint

    si revenu_fiscal_reference_conjoint est égal à 0 alors masquer le champ documents_avis_imposition_conjoint

    fichier documents_avis_imposition_autre

    si revenu_fiscal_autre est vide alors masquer le champ documents_avis_imposition_autre

    si revenu_fiscal_autre est égal à 0 alors masquer le champ documents_avis_imposition_autre

    fichier* documents_devis_detailles
        aide à la saisie documents_devis_detailles_aide

    texte documents_autorisations_secteur_sauvegarde_texte

    fichier documents_autorisations_secteur_sauvegarde

    champ_texte_long documents_autres_observations